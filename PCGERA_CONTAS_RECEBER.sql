CREATE OR REPLACE PROCEDURE PCGERA_CONTAS_RECEBER(PCOD_TERMINAL      VARCHAR,
                                                  PCOD_APLICACAO     VARCHAR,
                                                  PCOD_EMPRESA       NUMBER,
                                                  PCOD_OS            NUMBER,
                                                  PCOD_CLIENTE       NUMBER,
                                                  PCOD_TIPO_PGTO     NUMBER,
                                                  PVLR_TOTAL_DESPESA NUMBER,
                                                  PQTDE_PARCELAS     NUMBER,
                                                  PDATA_PAGAMENTO    DATE,
                                                  PEFETIVO           VARCHAR2 DEFAULT 'N') IS
   IQTDE                    NUMBER;
   IQTDE_PARCELAS           NUMBER;
   IQTDE_DIAS               TBTIPO_PAGAMENTO.QTDE_DIAS_ENTRE_FATURAS%TYPE;
   DDATA_VENCIMENTO         DATE;
   ISEQ_LOOP                NUMBER;
   IVLR_PARCELA             NUMBER;
   IDIFERENCA               TBCONTAS_PAGAR.VALOR_DESPESA%TYPE;
   ISEQ                     NUMBER;
   IVLR_CALCULADO           NUMBER;
   ICOD_BANCO_RECEBIMENTO   NUMBER;
   ICOD_AGENCIA_RECEBIMENTO NUMBER;
   ICOD_CONTA_RECEBIMENTO   NUMBER;
   SSITUACAO                VARCHAR2(1);

BEGIN
   DELETE FROM TBTEMPORARIA
   WHERE CODTERMINAL = PCOD_TERMINAL AND
         COD_APLICACAO = PCOD_APLICACAO;

   IQTDE_PARCELAS   := PQTDE_PARCELAS;
   DDATA_VENCIMENTO := PDATA_PAGAMENTO;
   ISEQ             := 0; -- SEQUENCIA (CHAVE) 
   ISEQ_LOOP        := 0; -- NUMERO DE PARCELAS
   IVLR_PARCELA     := ROUND(PVLR_TOTAL_DESPESA / PQTDE_PARCELAS, 2); -- CALCULA O VALOR DA PARCELA

   SELECT A.QTDE_DIAS_ENTRE_FATURAS
   INTO IQTDE_DIAS -- VERIFICA QUAL SERA A DIFERENCA DE DIAS ENTRE AS PARCELAS
   FROM TBTIPO_PAGAMENTO A
   WHERE A.COD_EMPRESA = PCOD_EMPRESA AND
         A.COD_TP_PAGAMENTO = PCOD_TIPO_PGTO AND
         A.SITUACAO = 'A';

   /*   SELECT NVL(MAX(A.SEQUENCIA), 0) + 1 -- AQUI VERIFICO SE JA TEM UMA DESPESA LANÇADA NESSA DATA DE VENCIMENTO , SE TIVER GERO UMA SEQUENCIA NOVA
   INTO ISEQ
   FROM TBCONTAS_PAGAR A
   WHERE A.COD_EMPRESA = PCOD_EMPRESA AND
         A.COD_DESPESA = PCOD_DESPESA;*/
   IF PEFETIVO = 'S' THEN
      SELECT COUNT(1)
      INTO IQTDE
      FROM TBCONTAS_RECEBER A
      WHERE A.COD_OS = PCOD_OS AND
            A.COD_EMPRESA = PCOD_EMPRESA AND
            A.COD_CLIENTE = PCOD_CLIENTE AND
            A.COD_TP_PAGAMENTO = PCOD_TIPO_PGTO;
   
      IF IQTDE > 0 THEN
         RAISE_APPLICATION_ERROR(-20000, 'Ordem de serviço já possui parcela geradas.');
      END IF;
   END IF;
   WHILE IQTDE_PARCELAS > 0
   LOOP
      ISEQ_LOOP := ISEQ_LOOP + 1;
      IF ISEQ_LOOP <> 1 THEN
         --- NA PRIMEIRA PARCELA NÃO ADICIONA OS DIAS , NAS OUTRAS ENTRA E ADICIONA OS DIAS
         DDATA_VENCIMENTO := DDATA_VENCIMENTO + IQTDE_DIAS;
      END IF;
      IF PEFETIVO = 'S' THEN
         SELECT X.COD_BANCO, X.COD_AGENCIA, X.COD_CONTA_BANCO
         INTO ICOD_BANCO_RECEBIMENTO, ICOD_AGENCIA_RECEBIMENTO, ICOD_CONTA_RECEBIMENTO
         FROM TBCLIENTE X
         WHERE X.COD_EMPRESA = PCOD_EMPRESA AND
               X.COD_CLIENTE = PCOD_CLIENTE;
               
         SSITUACAO := 'E';
        /* IF TRUNC(DDATA_VENCIMENTO) = TRUNC(SYSDATE) THEN
            SSITUACAO := 'P';
         END IF;*/
         BEGIN
            INSERT INTO TBCONTAS_RECEBER
               (COD_OS, COD_EMPRESA, COD_CLIENTE, COD_TP_PAGAMENTO, SEQUENCIA, TOTAL_PARCELA, SITUACAO,
                COD_CONDICAO_PAGAMENTO, DATA_VENCIMENTO, COD_BANCO_RECEBIMENTO, COD_AGENCIA_RECEBIMENTO,
                COD_CONTA_RECEBIMENTO)
            VALUES
               (PCOD_OS, PCOD_EMPRESA, PCOD_CLIENTE, PCOD_TIPO_PGTO, ISEQ_LOOP, TRUNC(IVLR_PARCELA, 2),
                SSITUACAO /*'E'*/, PCOD_TIPO_PGTO, TRUNC(DDATA_VENCIMENTO), ICOD_BANCO_RECEBIMENTO,
                ICOD_AGENCIA_RECEBIMENTO, ICOD_CONTA_RECEBIMENTO);
         EXCEPTION
            WHEN DUP_VAL_ON_INDEX THEN
               BEGIN
                  NULL;
               END;
         END;
      ELSIF PEFETIVO = 'N' THEN
         BEGIN
            INSERT INTO TBTEMPORARIA
               (CODTERMINAL, COD_APLICACAO, TIPOREGISTRO, CHAVE, NUM1, NUM2, NUM3, NUM4, ALFA1, DATA1)
            VALUES
               (PCOD_TERMINAL, PCOD_APLICACAO, 'E',
                PCOD_EMPRESA || PCOD_OS || TO_CHAR(TRUNC(DDATA_VENCIMENTO)), PCOD_EMPRESA, PCOD_OS,
                TRUNC(IVLR_PARCELA, 2), ISEQ_LOOP, 'E', TRUNC(DDATA_VENCIMENTO));
         EXCEPTION
            WHEN DUP_VAL_ON_INDEX THEN
               BEGIN
                  NULL;
               END;
         END;
      END IF;
      IQTDE_PARCELAS := IQTDE_PARCELAS - 1;
   END LOOP;
   --   COMMIT;
   IF PEFETIVO = 'S' THEN
      SELECT SUM(TOTAL_PARCELA)
      INTO IVLR_CALCULADO
      FROM TBCONTAS_RECEBER
      WHERE COD_OS = PCOD_OS AND
            COD_EMPRESA = PCOD_EMPRESA AND
            COD_CLIENTE = PCOD_CLIENTE AND
            COD_TP_PAGAMENTO = PCOD_TIPO_PGTO;
   
      IDIFERENCA := PVLR_TOTAL_DESPESA - IVLR_CALCULADO;
   
      IF IDIFERENCA <> 0 THEN
         UPDATE TBCONTAS_RECEBER
         SET TOTAL_PARCELA = TOTAL_PARCELA + IDIFERENCA --- AQUI MESMO SE A DIFERENÇA FOR MENOR DO  0 , PELO JOGO DE SINAL ELE DIMINUI DO TOTAL
         WHERE COD_OS = PCOD_OS AND
               COD_EMPRESA = PCOD_EMPRESA AND
               COD_CLIENTE = PCOD_CLIENTE AND
               COD_TP_PAGAMENTO = PCOD_TIPO_PGTO AND
               SEQUENCIA = (SELECT MAX(SEQUENCIA)
                            FROM TBCONTAS_RECEBER
                            WHERE COD_OS = PCOD_OS AND
                                  COD_EMPRESA = PCOD_EMPRESA AND
                                  COD_CLIENTE = PCOD_CLIENTE AND
                                  COD_TP_PAGAMENTO = PCOD_TIPO_PGTO);
      END IF;
      ---- AQUI VOU FINALIZAR A OS NAS TABELAS 
      UPDATE TBHEADERORDEM_SERVICO Z
      SET Z.SITUACAO     = 'F',
          Z.DATA_TERMINO = TRUNC(SYSDATE)
      WHERE Z.COD_OS = PCOD_OS AND
            Z.COD_EMPRESA = PCOD_EMPRESA AND
            Z.COD_CLIENTE = PCOD_CLIENTE;
   
   
   ELSIF PEFETIVO = 'N' THEN
      SELECT SUM(NUM3)
      INTO IVLR_CALCULADO
      FROM TBTEMPORARIA
      WHERE CODTERMINAL = PCOD_TERMINAL AND
            COD_APLICACAO = PCOD_APLICACAO;
   
      IDIFERENCA := PVLR_TOTAL_DESPESA - IVLR_CALCULADO;
   
      IF IDIFERENCA <> 0 THEN
         UPDATE TBTEMPORARIA
         SET NUM3 = NUM3 + IDIFERENCA --- AQUI MESMO SE A DIFERENÇA FOR MENOR DO  0 , PELO JOGO DE SINAL ELE DIMINUI DO TOTAL
         WHERE CODTERMINAL = PCOD_TERMINAL AND
               COD_APLICACAO = PCOD_APLICACAO AND
               NUM4 = (SELECT MAX(NUM4)
                       FROM TBTEMPORARIA
                       WHERE CODTERMINAL = PCOD_TERMINAL AND
                             COD_APLICACAO = PCOD_APLICACAO);
      END IF;
   END IF;
END;
/
