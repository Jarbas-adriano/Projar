CREATE OR REPLACE PROCEDURE PCGERA_CONTAS_PAGAR(PCOD_TERMINAL      VARCHAR,
                                                PCOD_APLICACAO     VARCHAR,
                                                PCOD_EMPRESA       NUMBER,
                                                PCOD_DESPESA       NUMBER,
                                                PCOD_TIPO_PGTO     NUMBER,
                                                PVLR_TOTAL_DESPESA NUMBER,
                                                PQTDE_PARCELAS     NUMBER,
                                                PDATA_PAGAMENTO    DATE,
                                                POBSERVACAO        VARCHAR2,
                                                PSEQUENCIA         OUT NUMBER,
                                                PEFETIVO           VARCHAR2 DEFAULT 'N') IS
   IQTDE_PARCELAS   NUMBER;
   IQTDE_DIAS       TBTIPO_PAGAMENTO.QTDE_DIAS_ENTRE_FATURAS%TYPE;
   DDATA_VENCIMENTO DATE;
   ISEQ_LOOP        NUMBER;
   IVLR_PARCELA     NUMBER;
   IDIFERENCA       TBCONTAS_PAGAR.VALOR_DESPESA%TYPE;
   ISEQ             NUMBER;
   IVLR_CALCULADO   NUMBER;

BEGIN

   DELETE FROM TBTEMPORARIA
   WHERE CODTERMINAL = PCOD_TERMINAL AND
         COD_APLICACAO = PCOD_APLICACAO;

   IQTDE_PARCELAS   := PQTDE_PARCELAS;
   DDATA_VENCIMENTO := PDATA_PAGAMENTO;
   ISEQ             := 0; -- SEQUENCIA (CHAVE) 
   ISEQ_LOOP        := 0; -- NUMERO DE PARCELAS
   IVLR_PARCELA     := ROUND(PVLR_TOTAL_DESPESA / PQTDE_PARCELAS, 2); -- CALCULA O VALOR DA PARCELA

   SELECT A.QTDE_DIAS_ENTRE_FATURAS
   INTO IQTDE_DIAS -- VERIFICA QUAL SERA A DIFERENCA DE DIAS ENTRE AS PARCELAS
   FROM TBTIPO_PAGAMENTO A
   WHERE A.COD_EMPRESA = PCOD_EMPRESA AND
         A.COD_TP_PAGAMENTO = PCOD_TIPO_PGTO AND
         A.SITUACAO = 'A';

   SELECT NVL(MAX(A.SEQUENCIA), 0) + 1 -- AQUI VERIFICO SE JA TEM UMA DESPESA LANÇADA NESSA DATA DE VENCIMENTO , SE TIVER GERO UMA SEQUENCIA NOVA
   INTO ISEQ
   FROM TBCONTAS_PAGAR A
   WHERE A.COD_EMPRESA = PCOD_EMPRESA AND
         A.COD_DESPESA = PCOD_DESPESA;

   WHILE IQTDE_PARCELAS > 0
   LOOP
      ISEQ_LOOP := ISEQ_LOOP + 1;
      IF ISEQ_LOOP <> 1 THEN
         --- NA PRIMEIRA PARCELA NÃO ADICIONA OS DIAS , NAS OUTRAS ENTRA E ADICIONA OS DIAS
         DDATA_VENCIMENTO := DDATA_VENCIMENTO + IQTDE_DIAS;
      END IF;
      IF PEFETIVO = 'S' THEN
         BEGIN
            INSERT INTO TBCONTAS_PAGAR
               (COD_EMPRESA, COD_DESPESA, VALOR_DESPESA, SITUACAO, DATA_VENCIMENTO, DATA_CADASTRO, SEQUENCIA,
                OBSERVACAO, NUM_PARCELA)
            VALUES
               (PCOD_EMPRESA, PCOD_DESPESA, IVLR_PARCELA, 'E', TRUNC(DDATA_VENCIMENTO), SYSDATE, ISEQ,
                POBSERVACAO, ISEQ_LOOP);
         EXCEPTION
            WHEN DUP_VAL_ON_INDEX THEN
               BEGIN
                  NULL;
               END;
         END;
      ELSIF PEFETIVO = 'N' THEN
         BEGIN
            INSERT INTO TBTEMPORARIA
               (CODTERMINAL, COD_APLICACAO, TIPOREGISTRO, CHAVE, NUM1, NUM2, NUM3, NUM4, ALFA1, DATA1)
            VALUES
               (PCOD_TERMINAL, PCOD_APLICACAO, 'E',
                PCOD_EMPRESA || PCOD_DESPESA || TO_CHAR(TRUNC(DDATA_VENCIMENTO)), PCOD_EMPRESA, PCOD_DESPESA,
                IVLR_PARCELA, ISEQ_LOOP, 'E', TRUNC(DDATA_VENCIMENTO));
         EXCEPTION
            WHEN DUP_VAL_ON_INDEX THEN
               BEGIN
                  NULL;
               END;
         END;
      END IF;
      IQTDE_PARCELAS := IQTDE_PARCELAS - 1;
   END LOOP;
   --   COMMIT;
   IF PEFETIVO = 'S' THEN
      SELECT SUM(VALOR_DESPESA)
      INTO IVLR_CALCULADO
      FROM TBCONTAS_PAGAR
      WHERE COD_EMPRESA = PCOD_EMPRESA AND
            COD_DESPESA = PCOD_DESPESA AND
            SEQUENCIA = ISEQ;
   
      IDIFERENCA := PVLR_TOTAL_DESPESA - IVLR_CALCULADO;
   
      IF IDIFERENCA <> 0 THEN
         UPDATE TBCONTAS_PAGAR
         SET VALOR_DESPESA = VALOR_DESPESA + IDIFERENCA --- AQUI MESMO SE A DIFERENÇA FOR MENOR DO  0 , PELO JOGO DE SINAL ELE DIMINUI DO TOTAL
         WHERE COD_EMPRESA = PCOD_EMPRESA AND
               COD_DESPESA = PCOD_DESPESA AND
               SEQUENCIA = ISEQ AND
               NUM_PARCELA = (SELECT MAX(NUM_PARCELA)
                              FROM TBCONTAS_PAGAR
                              WHERE COD_EMPRESA = PCOD_EMPRESA AND
                                    COD_DESPESA = PCOD_DESPESA AND
                                    SEQUENCIA = ISEQ);
      END IF;
   ELSIF PEFETIVO = 'N' THEN
      SELECT SUM(NUM3)
      INTO IVLR_CALCULADO
      FROM TBTEMPORARIA
      WHERE CODTERMINAL = PCOD_TERMINAL AND
            COD_APLICACAO = PCOD_APLICACAO;
   
      IDIFERENCA := PVLR_TOTAL_DESPESA - IVLR_CALCULADO;
   
      IF IDIFERENCA <> 0 THEN
         UPDATE TBTEMPORARIA
         SET NUM3 = NUM3 + IDIFERENCA --- AQUI MESMO SE A DIFERENÇA FOR MENOR DO  0 , PELO JOGO DE SINAL ELE DIMINUI DO TOTAL
         WHERE CODTERMINAL = PCOD_TERMINAL AND
               COD_APLICACAO = PCOD_APLICACAO AND
               NUM4 = (SELECT MAX(NUM4)
                       FROM TBTEMPORARIA
                       WHERE CODTERMINAL = PCOD_TERMINAL AND
                             COD_APLICACAO = PCOD_APLICACAO);
      END IF;
   END IF;
   PSEQUENCIA := ISEQ;
END;
/
